// Note: the Mockifer C++ source sets are configured via CMakeLists.txt.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 3
        versionName "3.0"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                arguments "-DANDROID_STL=c++_static"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

def productDir = "${projectDir}/../../../products/libs/android"
def outputDir = "${projectDir}/build/outputs/aar"

task mockiferBuildFramework {
    group "Mockifer"
    description "Build the Android .AAR engine. The framework will be generated in the /products/libs/android folder."
    dependsOn = ["cleanBuildCache", "assembleRelease"]

    doLast {
        // Update the Android framework product file itself.
        delete("${productDir}/mockifer.aar")
        copy {
            from new File("${outputDir}/android-release.aar")
            into "${productDir}"
            rename { String fileName ->
                fileName.replace('android-release.aar', 'mockifer.aar')
            }
        }
    }
}
