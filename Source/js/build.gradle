buildscript {
    // Using early access preview of 1.1.4 to get Javascript minifying. Once version
    // 1.1.4 is stable, there will be no need for the additional Maven repositories.
    ext.kotlin_version = '1.1.4-eap-77'
    repositories {
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'mockifer'
version '0.0.1'

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'

def outputDir = 'out'
def buildDir = 'build'
def productDir = "${projectDir}/../../Products/js/mockifer-js"

compileKotlin2Js {
    kotlinOptions.sourceMap = true
}

// Need to keep the Mockifer methods that are called from the 'api.js' so they don't
// get minified out of the build.
runDceKotlinJs.keep "js_main.mockiferProcessRequest"
runDceKotlinJs.keep "js_main.mockiferReset"

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

clean.doFirst() {
    delete("${outputDir}")
    delete("${buildDir}")
    delete("${productDir}")
}

build.doFirst() {
    delete("${productDir}")
}

processResources.doFirst() {
    delete("${buildDir}/resources")
    delete("${outputDir}/mockifer")
}

build.doLast() {
    configurations.compile.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${projectDir}/${outputDir}"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") || (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }

    copy {
        File artifact = new File("build/libs/${project.name}-${project.version}.jar")
        includeEmptyDirs = false

        from zipTree(artifact)
        into "${outputDir}"
        include { fileTreeElement ->
            def path = fileTreeElement.path
            !(path.startsWith("META-INF/") || path.startsWith("${project.name}"))
        }
    }

    copy {
        includeEmptyDirs = true
        from new File("${outputDir}/mockifer/data")
        into "${productDir}/data"
    }

    copy {
        from new File("${outputDir}/mockifer/api.js")
        into "${productDir}"
    }

    copy {
        from new File("${outputDir}/mockifer/utils.js")
        into "${productDir}"
    }

    copy {
        from new File("${outputDir}/mockifer/manifest.json")
        into "${productDir}"
    }

    copy {
        from new File("${outputDir}/mockifer/routes.json")
        into "${productDir}"
    }

    copy {
        from new File("${buildDir}/classes/main/min/js_main.js")
        into "${productDir}"
        rename { String fileName ->
            fileName.replace('js_main.js', 'mockifer.js')
        }
    }

    copy {
        from new File("${buildDir}/classes/main/min/kotlin.js")
        into "${productDir}"
    }

    // Write a properties file that has some dynamic elements such as most recent build time.
    def properties = new Properties()
    def propertiesFile = new File("${productDir}/mockifer.properties")
    properties.setProperty('last-build-time', System.currentTimeMillis() as String)
    properties.store(propertiesFile.newWriter(), null)
}
